%Entrypoint <- Spacing %Definition+ EOF
%Definition <-  %(Label/Node) LEFTARROW %ParseExpr
ParseExpr  <- %Choice / %Sequence / (%Lookahead / %NotLookahead / %Argument) / (%ZeroOrOne / %ZeroOrMore / %OneOrMore) / %Primary

%Choice   <- %ParseExpr:1 (SLASH %ParseExpr:1)+
%Sequence <- %ParseExpr:2 (%ParseExpr:2)+

%Lookahead    <- AMP  %ParseExpr:3
%NotLookahead <- BANG %ParseExpr:3
%Argument     <- ARG  %ParseExpr:3

%ZeroOrOne  <- %ParseExpr:4 QUESTION
%ZeroOrMore <- %ParseExpr:4 STAR
%OneOrMore  <- %ParseExpr:4 PLUS

Primary <- (OPEN %ParseExpr CLOSE) / %Index / (%Label !LEFTARROW) / %String / %Dot

%Node <- ARG %Label
%Index  <- %Label ':' %[9876543210]+ Spacing
%Label <- %([abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ_] [abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ_9876543210]*) Spacing

Spacing <- (SPACE / Comment)*
Comment <- '#' (!EOL .)* EOL
LEFTARROW  <- '<-' Spacing
SLASH     <- '/' Spacing
ARG       <- '%' Spacing
AMP       <- '&' Spacing
BANG      <- '!' Spacing
QUESTION  <- '?' Spacing
STAR      <- '*' Spacing
PLUS      <- '+' Spacing
OPEN      <- %'(' Spacing
CLOSE     <- ')' Spacing
%Dot      <- '.' Spacing
SPACE <- ' ' / '\t' / EOL
EOL <- '\r\n' / '\r' / '\n'
EOF <- !.

%String <- ('"' %(!'"' Char)* '"' / "'" %(!"'" Char)* "'" /'[' %((!']' Char)+) ']') Spacing
Char <- %('\\'  [nrt'[\]"\\] / '\\' [012] [01234567] [01234567] / '\\' [01234567] [01234567]? / !'\\' .)
