%Entrypoint <- Spacing %Definition+ EOF
%Definition <-  %(Label/Node) LEFTARROW %ParseExpr
ParseExpr  <- %Choice / %Sequence / (%Lookahead / %NotLookahead / %Argument) / (%ZeroOrOne / %ZeroOrMore / %OneOrMore) / %Primary

%Choice   <- %ParseExpr:1 (SLASH %ParseExpr:1)+
%Sequence <- %ParseExpr:2 (%ParseExpr:2)+

%Lookahead    <- AMP  %ParseExpr:3
%NotLookahead <- BANG %ParseExpr:3
%Argument     <- ARG  %ParseExpr:3

%ZeroOrOne  <- %ParseExpr:4 QUESTION
%ZeroOrMore <- %ParseExpr:4 STAR
%OneOrMore  <- %ParseExpr:4 PLUS

Primary <- (OPEN %ParseExpr CLOSE) / %Index / (%Label !LEFTARROW) / %String / %CharClass / %Dot

%Node <- ARG %Label
%Index  <- %Label ':' %[0-9]+ Spacing
%Label <- %([a-zA-Z_] [a-zA-Z_0-9]*) Spacing

Spacing <- (SPACE / Comment)*
Comment <- '#' (!EOL .)* (EOL/EOF)
LEFTARROW  <- '<-' Spacing
SLASH     <- '/' Spacing
ARG       <- '%' Spacing
AMP       <- '&' Spacing
BANG      <- '!' Spacing
QUESTION  <- '?' Spacing
STAR      <- '*' Spacing
PLUS      <- '+' Spacing
OPEN      <- %'(' Spacing
CLOSE     <- ')' Spacing
%Dot      <- '.' Spacing
SPACE <- ' ' / '\t' / EOL
EOL <- '\r\n' / '\r' / '\n'
EOF <- !.

# String and CharClass can be thought of as literal forms of Char-specific Sequence and Choice.
# They are not strictly necessary, but do make things cleaner
%String <- ('"' %(!'"' Char)* '"' / "'" %(!"'" Char)* "'") Spacing
%CharClass <- '[' (%(Char '-' Char) / %Char) (!']' (%(Char '-' Char) / %Char))* ']' Spacing

# this needs to be matched by an escape/unescape function when squaring the circle
Char <- %('\\'  [][nrt'"\\] / '\\' [0-2] [0-7] [0-7] / '\\' [0-7] [0-7]? / !'\\' .)
